import( "../build/input/extracted/field/script/field.bf" );
import( "../modMenu.flow" );

//EXTENSIONS
import ( "../findAFriend.flow" ); //Fast travel to social link locations (Unneccessary to comment out)
import ( "../QuickTravelPlus.flow" ); //Extended quickTravel locations for town/school (Comment out to disable)
import ( "../VRGameOverSkip.flow" ); //Skips velvet room scene after game over (Comment out to disable)

void order_party_hook()
{
	//Change these values to enable/disable
	bool modMenu = true; //Mod menu with many different debugging options
	bool quickTravel = true; //Normal square menu (enhanced with QuickTravelPlus)
	bool mobileCalendar = true; //Quickly open Calendar
	bool findAFriend = true; //Fast travel to social link locations
	bool saveAnywhere = true; //Save anywhere

	//Just Mod Menu (So it's not Mod Menu and Exit)
	if (modMenu && !quickTravel && !mobileCalendar && !findAFriend)
		ModMenuDisplay();
	//Just QuickTravel (Only useful if you have QuickTravelPlus uncommented) (So it's not QuickTravel and Exit)
	else if (!modMenu && quickTravel && !mobileCalendar && !findAFriend)
	{
		if ( GET_FLOOR_ID() > 0)
		{
			dungeon_order();
		}
		else
		{
			field_order();
		}
	}
	//Multiple options
	else 
	{
		int dungeon = GET_FLOOR_ID();
		int fieldMajor = FLD_FUNCTION_0022();
		
		if (dungeon > 0 || ((fieldMajor >= 6 && fieldMajor <= 11) || (fieldMajor == 17 || fieldMajor == 18)))
		{
			int SelectMask = 0;
			if (!quickTravel)
				SelectMask = SelectMask + 1;
			if ( GET_FLOOR_ID() > 0 || !findAFriend)
				SelectMask = SelectMask + 2;
			if (!mobileCalendar)
				SelectMask = SelectMask + 4;
			if (!saveAnywhere)
				SelectMask = SelectMask + 8;
			if (!modMenu)
				SelectMask = SelectMask + 16;
			//Prompt user to choose an option if in an appropriate area
			int selection = ADV_SEL( Square_Menu_Text, Square_Menu, SelectMask);
			switch (selection)
			{
				case 0: //QuickTravel
					if ( GET_FLOOR_ID() > 0)
					{
						dungeon_order();
					}
					else
					{
						field_order();
					}
					break;
				case 1: //FindAFriend
					generate_hangout_menu();
					break;
				case 2: //MobileCalendar
					OPEN_CALENDAR();
					break;
				case 3: //SaveAnywhere
					NS_SAVE();
					break;
				case 4: //ModMenu
					ModMenuDisplay();
					break;
				case 5: //Exit
					break;
			}
		}
		else if(modMenu && GET_FLOOR_ID() == 0)
		{
			//Show Mod Menu by default if not in an area with QuickTravel options
			ModMenuDisplay();
		}
	}
}
