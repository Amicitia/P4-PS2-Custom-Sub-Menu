import( "../build/input/init_free/field/script/field.bf" );
import( "../ModMenu/ModMenu.flow" );

//EXTENSIONS
import ( "../OtherMods/FindAFriend.flow" ); //Fast travel to social link locations (Unneccessary to comment out)
import ( "../OtherMods/QuickTravelPlus.flow" ); //Extended quickTravel locations for town/school (Comment out to disable)
import ( "../OtherMods/VRGameOverSkip.flow" ); //Skips velvet room scene after game over (Comment out to disable)
import ( "../OtherMods/DungeonOptions/DungeonOptions.flow" ); //Fast travel to any dungeon floor after their deadlines pass
	//and organize party on any floor (Comment out to disable)
import ( "../OtherMods/ConsistentReaperField.flow" ); //Hooks field functions for consistent reaper 
	//if used with DungeonTravel make sure to set ConsistentReaper bool to true in SelectFloor() (DungeonTravel.flow) and use_gohom (GohoM.flow)
	//also build dungeon.bf.flow with ConsistentReaper.flow uncommented in the imports
import ( "../OtherMods/MobileCalendar.flow" );

void order_party_hook()
{
	//Change these values to enable/disable
	bool modMenu = true; //Mod menu with many different debugging options
	bool quickTravel = true; //Normal square menu (enhanced with QuickTravelPlus)
	bool mobileCalendar = true; //Quickly open Calendar
	bool findAFriend = true; //Fast travel to social link locations
	bool saveAnywhere = true; //Save anywhere

	//Just Mod Menu (So it's not Mod Menu and Exit)
	if (modMenu && !quickTravel && !mobileCalendar && !findAFriend)
		ModMenuDisplay();
	//Just QuickTravel (Only useful if you have QuickTravelPlus uncommented) (So it's not QuickTravel and Exit)
	else if (!modMenu && quickTravel)
	{
		if ( GET_FLOOR_ID() > 0)
		{
			dungeon_order();
		}
		else
		{
			field_order();
		}
	}
	//Multiple options
	else 
	{
		int dungeon = GET_FLOOR_ID();
		int fieldMajor = GET_FIELD_MAJOR();
		
		
		if (dungeon > 0 || ((fieldMajor >= 6 && fieldMajor <= 11) || (fieldMajor == 17 || fieldMajor == 18)))
		{
			int SelectMask = 0;
			if (!quickTravel)
				SelectMask = SelectMask + 1;
			if (!saveAnywhere)
				SelectMask = SelectMask + 2;
			if (!modMenu)
				SelectMask = SelectMask + 4;
			
			// Enable shortcut to close
			SET_SEL_CHOICE_KEYBIND( 14, 3 );
			//Prompt user to choose an option if in an appropriate area
			int selection = 0;
			if ( GET_FLOOR_ID() > 0)
				selection = ADV_SEL( Square_Menu_Text, Square_Menu_Dng, SelectMask);
			else
				selection = ADV_SEL( Square_Menu_Text, Square_Menu, SelectMask);
			switch (selection)
			{
				case 0: //QuickTravel
					if ( GET_FLOOR_ID() > 0)
					{
						dungeon_order();
					}
					else
					{
						field_order();
					}
					break;
				case 1: //SaveAnywhere
					NS_SAVE();
					break;
				case 2: //ModMenu
					ModMenuDisplay();
					break;
				case 3: //Exit
					break;
			}
		}
		else if(modMenu && GET_FLOOR_ID() == 0)
		{
			//Show Mod Menu by default if not in an area with QuickTravel options
			ModMenuDisplay();
		}
	}
}
